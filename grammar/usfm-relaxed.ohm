usfmRelaxed{
	File = BookHead Chapter+

	BookHead = idMarker NormalMarker*
	Chapter = ChapterMarker (NormalMarker | VerseMarker)+

	verseNumber = number letter? ("-" number letter?)? 
	markerName = ~(cTag) ~(vTag) "+"? char+ number?
    attributes = "|" (~(backSlash) any)*

	backSlash = "\\"
	newLine = ("\r" | "\n")+
	spaceChar = " "
	char = ~(backSlash) ~(newLine) ~spaceChar ~("|")  ~("*")any
	number = digit+
	word = spaceChar? char+ spaceChar?
	text =  newLine? word+

	cTag = "c" spaceChar
	vTag = "v" spaceChar

	idMarker = newLine? backSlash "id" spaceChar word text?
	ChapterMarker = newLine? backSlash cTag number
	VerseMarker = newLine? backSlash vTag verseNumber  (text | ClosedMarker | NormalMarker)*
	ClosedMarker = backSlash markerName (text | ClosedMarker)* attributes? backSlash markerName "*"
	NormalMarker = backSlash markerName text* 
}