usfmBible{

			File = scripture end

			backSlash = "\\"
			newLine = ("\r" | "\n")+
			spaceChar = " "
			char = ~(backSlash) ~(newLine) ~spaceChar any
			//char = letter | digit | punctuations 
			//punctuations = "!" | "\"" | "#" | "$" | "%" | "&" | "(" | ")"  | "+" | "-" | "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "]" | "^" | "_" | "`" | "{"  | "}"  | "’" | "‘" | "“" | "”" | "।" | "]" | "~"
			text = word+
			number = digit+
			word = char+ spaceChar?

			bookCode = "GEN" | "EXO" | "LEV" | "NUM" | "DEU" | "JOS" | "JDG" | "RUT" | "1SA" | "2SA" | "1KI" | "2KI" | 
							"1CH" | "2CH" | "EZR" | "NEH" | "EST" | "JOB" | "PSA" | "PRO" | 
							"ECC" | "SNG" | "ISA" | "JER" | "LAM" | "EZK" | "DAN" | 
							"HOS" | "JOL" | "AMO" | "OBA" | "JON" | "MIC" | "NAM" | "HAB" | 
							"ZEP" | "HAG" | "ZEC" | "MAL" | "MAT" | "MRK" | "LUK" | 
							"JHN" | "ACT" | "ROM" | "1CO" | "2CO" | "GAL" | "EPH" | "PHP" | 
							"COL" | "1TH" | "2TH" | "1TI" | "2TI" | "TIT" | "PHM" | 
							"HEB" | "JAS" | "1PE" | "2PE" | "1JN" | "2JN" | "3JN" | "JUD" | 
							"REV" | "TOB" | "JDT" | "ESG" | "WIS" | "SIR" | "BAR" | 
							"LJE" | "S3Y" | "SUS" | "BEL" | "1MA" | "2MA" | "3MA" | "4MA" | 
							"1ES" | "2ES" | "MAN" | "PS2" | "ODA" | "PSS" | "EZA" | 
							"5EZ" | "6EZ" | "DAG" | "PS3" | "2BA" | "LBA" | "JUB" | "ENO" | 
							"1MQ" | "2MQ" | "3MQ" | "REP" | "4BA" | "LAO" | "FRT" | 
							"BAK" | "OTH" | "INT" | "CNC" | "GLO" | "TDX" | "NDX"


			scripture = metaData content

			content = chapter+


			metaData = bookIdentification bookHeaders* bookTitles* bookIntroductionTitles* bookIntroductionEndTitles* 
							bookChapterLabel?

			bookIdentification = idElement usfmElement?

			bookHeaders = hElement | ideElement | remElement | tocElement | tocaElement

			bookTitles =  imtElement | remElement


			bookIntroductionTitles =  ibElement | ieElement | iexElement | iliElement |  
							imElement | imiElement | imqElement | imtElement | imtElement | 
							imteElement | imteElement | ioElement | iotElement | ipElement | ipiElement | ipqElement | iprElement | iqElement | isElement | remElement
			

			bookIntroductionEndTitles =   imtElement | mtElement 

			bookChapterLabel = clElement

			chapter = chapterHeader chapterContent+

			chapterHeader = cElement (caElement | cpElement | clElement | cdElement)*

			chapterContent =   section+
			
			section = sectionHeader verseElement+
 			
 			
 			sectionHeader  = (sectionPreHeader? sectionElement sectionPostHeader*)? paraElement
			sectionPreHeader = msElement mrElement?
			sectionPostHeader = srElement | rElement | dElement 
			verseElement = newLine verseMarker verseNumber (vaElement | vpElement)*  verseText
			
			verseMarker = backSlash "v" spaceChar

			verseNumber = number spaceChar

			sectionElement = sectionElementWithTitle | sectionElementWithoutTitle

			sectionElementWithTitle = sectionMarker chapterContentTextContent 
			sectionElementWithoutTitle = sdMarker 
			sectionMarker = backSlash ("s" | "s1" | "s2" | "s3" | "s4" )
			sdMarker = backSlash "sd" number

			paraElement = newLine backSlash paraMarker spaceChar*

			altVerseNumberElement = altVerseNumberMarker number altVerseNumberMarker "*"
			altVerseNumberMarker = backSlash "va" 

			publishedCharElement = publishedCharMarker text publishedCharMarker "*"  
			
			publishedCharMarker = backSlash "vp"
			verseText = (chapterContentTextContent | paraElement ) +
            

			paraMarker = paraUnNumberedMarker | paraNumberedMarker
			paraUnNumberedMarker =   ("p" | "m" | "po" | "pr" | "cls" | "pmo" | "pm" | "pmc" | "pmr" | "pmi" | "nb" | "pc" | "b" | "pb" | "qr" | "qc" | "qa" | "qd" | "lh" | "lf" )
			paraNumberedMarker =  ("pi" | "ph" | "q" | "qm" | "li" | "lim") number




			cElement = newLine backSlash "c" spaceChar number 
			caElement = newLine backSlash "ca" spaceChar number backSlash "ca*"
			cdElement = newLine backSlash "cd" spaceChar chapterContentTextContent
			clElement = newLine backSlash "cl"  spaceChar text
			cpElement = newLine backSlash "cp"  spaceChar text
			dElement = newLine backSlash "d"  spaceChar chapterContentTextContent
			
			hElement = newLine backSlash "h" text
			
			ibElement = newLine backSlash "ib" spaceChar 
			idElement = backSlash "id"  spaceChar bookCode  spaceChar text
			ideElement = newLine backSlash "ide"  spaceChar text
			ieElement = newLine backSlash "ie"
			iexElement = newLine backSlash "iex"  spaceChar text
			
			imElement = newLine backSlash "im" spaceChar bookIntroductionTitlesTextContent
			imiElement = newLine backSlash "imi" spaceChar bookIntroductionTitlesTextContent
			imqElement = newLine backSlash "imq" spaceChar bookIntroductionTitlesTextContent
			ili = iliElement+
			iliElement = newLine backSlash "ili" number? spaceChar bookIntroductionTitlesTextContent
			imt = imtElement+
			imtElement = newLine backSlash "imt" number? spaceChar bookIntroductionTitlesTextContent
			imte = imteElement+
			imteElement = newLine backSlash "imte" number? spaceChar bookIntroductionTitlesTextContent
			io = ioElement+
			ioElement = newLine backSlash "io" number? spaceChar bookIntroductionTitlesTextContent
			iotElement = newLine backSlash "iot" spaceChar bookIntroductionTitlesTextContent
			iorElement = newLine backSlash "ior" spaceChar bookIntroductionTitlesTextContent
			ipElement = newLine backSlash "ip" spaceChar bookIntroductionTitlesTextContent
			ipiElement = newLine backSlash "ipi" spaceChar bookIntroductionTitlesTextContent
			ipqElement = newLine backSlash "ipq" spaceChar bookIntroductionTitlesTextContent
			iprElement = newLine backSlash "ipr" spaceChar bookIntroductionTitlesTextContent
			iq = iqElement+
			iqElement = newLine backSlash "iq" number? spaceChar bookIntroductionTitlesTextContent
			isElement = newLine backSlash "is" number? spaceChar bookIntroductionTitlesTextContent
			
			remElement = newLine backSlash "rem" spaceChar chapterContentTextContent
			
			mrElement = newLine backSlash "mr" spaceChar chapterContentTextContent
			msElement = newLine backSlash "ms" number? spaceChar chapterContentTextContent
			mt = mtElement+
			mtElement = newLine backSlash "mt" number? spaceChar chapterContentTextContent
			rElement = newLine backSlash "r" spaceChar chapterContentTextContent
			
			srElement = newLine backSlash "sr" spaceChar chapterContentTextContent
			tocElement = newLine backSlash ("toc1" | "toc2" | "toc3") spaceChar text
			tocaElement = newLine backSlash ("toca1" | "toca2" | "toca3") spaceChar text
			usfmElement = newLine backSlash "usfm" spaceChar version
			version = number ("." number)?
			vaElement = backSlash "va" spaceChar number backSlash "va*"
			vpElement = backSlash "va" spaceChar text backSlash "va*"


			notesElement = footnoteElement | crossrefElement
			footnoteElement = fElement | feElement
			fElement = backSlash  "f"  any* backSlash "f*"
			feElement = backSlash  "fe"  any* backSlash "fe*"
			crossrefElement = backSlash  "x"  any* backSlash "x*"

			charElement = inLineCharElement | inLineCharAttributeElement | inLineCharNumberedElement
            inLineCharElement = backSlash "add" chapterContentTextContent backSlash "add" "*"
            			| backSlash "bk" chapterContentTextContent backSlash "bk" "*"
						| backSlash "dc" chapterContentTextContent backSlash "dc" "*"
                        | backSlash "+dc" chapterContentTextContent backSlash "+dc" "*"
						| backSlash "k" chapterContentTextContent backSlash "k" "*"
                        | backSlash "nd" chapterContentTextContent backSlash "nd" "*"
						| backSlash "ord" chapterContentTextContent backSlash "ord" "*"
               			| backSlash "pn" chapterContentTextContent backSlash "pn"  "*"
                        | backSlash "png" chapterContentTextContent backSlash "png"  "*"
                        | backSlash "addpn" chapterContentTextContent backSlash "addpn"  "*"
                        | backSlash "qt" chapterContentTextContent backSlash "qt"  "*"
                        | backSlash "sig" chapterContentTextContent backSlash "sig"  "*"
						| backSlash "sls" chapterContentTextContent backSlash "sls"  "*"
                        | backSlash "tl" chapterContentTextContent backSlash "tl"  "*"
                        | backSlash "wj" chapterContentTextContent backSlash "wj"  "*"
                        | backSlash "em" chapterContentTextContent backSlash "em"  "*"
                        | backSlash "bd" chapterContentTextContent backSlash "bd"  "*"
                        | backSlash "it" chapterContentTextContent backSlash "it"  "*"
                        | backSlash "bdit" chapterContentTextContent backSlash "bdit"  "*"
                        | backSlash "no" chapterContentTextContent backSlash "no"  "*"
                        | backSlash "sc" chapterContentTextContent backSlash "sc"  "*"
                        | backSlash "sup" chapterContentTextContent backSlash "sup"  "*"
                        | backSlash "ndx" chapterContentTextContent backSlash "ndx"  "*"
                        | backSlash "wg" chapterContentTextContent backSlash "wg"  "*"
                        | backSlash "wh" chapterContentTextContent backSlash "wh"  "*"
                        | backSlash "wa" chapterContentTextContent backSlash "wa"  "*"
                        | backSlash "qs" chapterContentTextContent backSlash "qs"  "*"
                        | backSlash "qac" chapterContentTextContent backSlash "qac"  "*"
                        | backSlash "litl" chapterContentTextContent backSlash "litl"  "*"
                        | backSlash "lik" chapterContentTextContent backSlash "lik"  "*"
                        
                        
            inLineCharAttributeElement = backSlash "rb" chapterContentTextContent attributes backSlash "rb"  "*"
                        | backSlash "w" chapterContentTextContent attributes backSlash "w"  "*"
                        | backSlash "jmp" chapterContentTextContent attributes backSlash "jmp"  "*"

            inLineCharNumberedElement = backSlash "liv" number? chapterContentTextContent backSlash "liv"  "*"
                        



            attributes = namedAttributeList   |  defaultAttribute 
            defaultAttribute = "|" "\"" attributeName "\""
            namedAttributeList = "|"  (text "=\"" attributeName "\"" spaceChar?)+
            attributeName = (char|"-")+
            
            figureElement = backSlash "fig" spaceChar caption  chapterContentTextContent attributes? backSlash "fig*"
			caption = text

			
			litElement = newLine backSlash "lit" spaceChar chapterContentTextContent


			bookIntroductionTitlesTextContent = (text | notesElement |  charElement | milestoneElement | figureElement | zNameSpace)+
			bookTitlesTextContent = (text | notesElement | charElement | zNameSpace)+
			chapterContentTextContent = (text | notesElement |  charElement | milestoneElement| figureElement | zNameSpace)+
			bookIntroductionEndTitlesTextContent = (text | notesElement | charElement | milestoneElement | zNameSpace)+

			milestoneElement = (backSlash "qt" number? "-s" spaceChar* attributes? backSlash "*" ) 
								| (backSlash "qt" number? "-e" spaceChar* attributes? backSlash "*" )
			                	| (backSlash "ts" number? "-s" spaceChar* attributes? backSlash "*" ) 
			                	| (backSlash "ts" number? "-e" spaceChar* attributes? backSlash "*" )

			zNameSpace = newLine? backSlash "z" char* spaceChar? text? (backSlash "z" char* "*" )?
}
 